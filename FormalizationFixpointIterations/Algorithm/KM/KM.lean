/-
Copyright (c) 2025 Jian Yu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jian Yu
-/
import FormalizationFixpointIterations.Algorithm.KM.Lemma
open Set Filter Topology TopologicalSpace Metric BigOperators Finset Function Nonexpansive_operator

set_option linter.unusedSectionVars false
set_option linter.unusedVariables false
set_option linter.style.longLine false
local notation "‚ü™" a‚ÇÅ ", " a‚ÇÇ "‚ü´" => @inner ‚Ñù _ _ a‚ÇÅ a‚ÇÇ

variable {H : Type*} [NormedAddCommGroup H] [InnerProductSpace ‚Ñù H]

/--
Krasnosel'skii-Mann iteration structure
-/
structure KM (D : Set H) (T : H ‚Üí H) where
  x0 : H
  hx0 : x0 ‚àà D
  stepsize : ‚Ñï ‚Üí ‚Ñù
  hstepsize : ‚àÄ n, stepsize n ‚àà Set.Icc (0 : ‚Ñù) 1
  hstepsize_sum : Tendsto (fun n => ‚àë i ‚àà range (n+1), stepsize i * (1 - stepsize i)) atTop atTop
  x : ‚Ñï ‚Üí H
  update : ‚àÄ n, x (n + 1) = x n + stepsize n ‚Ä¢ (T (x n) - x n)
  initial_value : x 0 = x0
  fix_T_nonempty : (FixOn T D).Nonempty


-- The formalization of Groetsch's theorem for Krasnosel'skii-Mann iteration

/--
The important inequalities (5.16) in the proof process\
`‚Äñx (n + 1) - y‚Äñ^2 ‚â§ ‚Äñx n - y‚Äñ^2- Œª n * (1 - Œª n) * ‚ÄñT (x n) - x n‚Äñ^2`.
Here km.stepsize n corresponds to Œª n in the paper.
-/
lemma key_inequality {D : Set H} (T : H ‚Üí H) (h_Im_T_in_D : ‚àÄ x ‚àà D, T x ‚àà D)
(hT_nonexpansive : ‚àÄ x y, ‚ÄñT x - T y‚Äñ ‚â§ ‚Äñx - y‚Äñ)
    (km : KM D T) :
    ‚àÄ (y : H) (hy : y ‚àà FixOn T D) (n : ‚Ñï),
      ‚Äñkm.x (n + 1) - y‚Äñ^2 ‚â§ ‚Äñkm.x n - y‚Äñ^2
      - km.stepsize n * (1 - km.stepsize n) * ‚ÄñT (km.x n) - km.x n‚Äñ^2 := by
    intro y hy n
    rcases hy with ‚ü®-, hyfix‚ü©
    --  obtain 0 ‚â§ s and s ‚â§ 1 from km.hstepsize n
    rcases km.hstepsize n with ‚ü®hs_nonneg, hs_le_one‚ü©
    calc
      ‚Äñkm.x (n + 1) - y‚Äñ^2
          = ‚Äñ(1 - km.stepsize n) ‚Ä¢ (km.x n - y) + km.stepsize n ‚Ä¢ (T (km.x n) - y)‚Äñ^2 := by
            rw [km.update n]
            simp only [smul_sub, sub_smul, one_smul]
            abel_nf
      _ = (1 - km.stepsize n) * ‚Äñkm.x n - y‚Äñ^2  + km.stepsize n * ‚ÄñT (km.x n) - y‚Äñ^2
          - km.stepsize n * (1 - km.stepsize n) * ‚Äñ(T (km.x n) - y) - ( km.x n - y)‚Äñ^2 := by
            -- apply Corollary_2_15 with arguments arranged to match this expression
            have h := convex_combination_norm_sq_identity (T (km.x n) - y) (km.x n - y) (km.stepsize n)
            -- swap the summands inside the norm so the lemma matches exactly
            have add_comm_eq : (1 - km.stepsize n) ‚Ä¢ (km.x n - y) + km.stepsize n ‚Ä¢ (T (km.x n) - y) =
            km.stepsize n ‚Ä¢ (T (km.x n) - y) + (1 - km.stepsize n) ‚Ä¢ (km.x n - y) := by simp [add_comm]
            rw [add_comm_eq]
            rw[eq_sub_iff_add_eq , h]
            ring
      _ ‚â§ (1 - km.stepsize n) * ‚Äñkm.x n - y‚Äñ^2 + km.stepsize n * ‚Äñkm.x n - y‚Äñ^2 -km.stepsize n * (1 - km.stepsize n) *‚Äñ(T (km.x n) - km.x n )‚Äñ^2  := by
          have hT_le : ‚ÄñT (km.x n) - y‚Äñ ‚â§ ‚Äñkm.x n - y‚Äñ := by
            nth_rw 1 [‚Üê hyfix]
            exact hT_nonexpansive (km.x n) y
          simp only [sub_sub_sub_cancel_right, tsub_le_iff_right, sub_add_cancel,
            add_le_add_iff_left, ge_iff_le]
          apply mul_le_mul_of_nonneg_left _ hs_nonneg
          refine pow_le_pow_left‚ÇÄ ?_ hT_le 2
          exact norm_nonneg _
      _ = ‚Äñkm.x n - y‚Äñ^2 - km.stepsize n * (1 - km.stepsize n) * ‚ÄñT (km.x n) - km.x n‚Äñ^2 := by
          ring

/--
Sequence `x` in KM algorithm is Fejer monotone with respect to Fix T.
-/
lemma groetsch_theorem_i {D : Set H} (hD_convex : Convex ‚Ñù D) (hD_closed : IsClosed D)
    (T : H ‚Üí H) (h_Im_T_in_D : ‚àÄ x ‚àà D, T x ‚àà D) (hT_nonexpansive : ‚àÄ x y, ‚ÄñT x - T y‚Äñ ‚â§ ‚Äñx - y‚Äñ)
    (km : KM D T) :
    IsFejerMonotone km.x (FixOn T D) := by
    intro y hy n
    rcases km.hstepsize n with ‚ü®hs_nonneg, hs_le_one‚ü©
    have calc1 :‚Äñkm.x (n + 1) - y‚Äñ ^ 2 ‚â§ ‚Äñkm.x n - y‚Äñ ^ 2 := by
      calc
      ‚Äñkm.x (n + 1) - y‚Äñ ^ 2 ‚â§ ‚Äñkm.x n - y‚Äñ ^ 2 - km.stepsize n * (1 - km.stepsize n) * ‚ÄñT (km.x n) - km.x n‚Äñ ^ 2 := by
        exact key_inequality T h_Im_T_in_D hT_nonexpansive km y hy n
      _‚â§ ‚Äñkm.x n - y‚Äñ ^ 2 := by
        have h_nonneg : 0 ‚â§ km.stepsize n * (1 - km.stepsize n) * ‚ÄñT (km.x n) - y - (km.x n - y)‚Äñ ^ 2 := by
          apply mul_nonneg
          ¬∑ apply mul_nonneg
            ¬∑ exact hs_nonneg
            ¬∑ exact sub_nonneg.mpr hs_le_one   -- 1 - s ‚â• 0
          ¬∑ exact pow_nonneg (norm_nonneg _) 2
        simp at h_nonneg
        linarith
    have := (sq_le_sq).mp calc1
    repeat rw[abs_of_nonneg (norm_nonneg _)] at this
    exact this

/--
Sequence `T (x n) - x n` in KM algorithm converges strongly to 0.
-/
lemma groetsch_theorem_ii {D : Set H} (hD_convex : Convex ‚Ñù D) (hD_closed : IsClosed D)
    (T : H ‚Üí H) (h_Im_T_in_D : ‚àÄ x ‚àà D, T x ‚àà D) (hT_nonexpansive : ‚àÄ x y, ‚ÄñT x - T y‚Äñ ‚â§ ‚Äñx - y‚Äñ)
    (km : KM D T) :
    (Tendsto (fun n ‚Ü¶ ‚ÄñT (km.x n) - km.x n‚Äñ)  atTop (ùìù 0)) := by
  rcases km.fix_T_nonempty with ‚ü®y0, hy0‚ü©
  have sum_bound : ‚àÄ N, ‚àë  i ‚àà range (N), km.stepsize i * (1 - km.stepsize i) * ‚ÄñT (km.x i) - km.x i‚Äñ ^ 2 ‚â§
      ‚Äñkm.x 0 - y0‚Äñ ^ 2 - ‚Äñkm.x (N) - y0‚Äñ ^ 2 := by
    intro N
    induction N with
    | zero => simp
    | succ N ih =>
      have hN := key_inequality T h_Im_T_in_D hT_nonexpansive km y0 hy0 N
      simp [Finset.sum_range_succ]
      linarith
  have partial_le : ‚àÄ N, ‚àë i ‚àà Finset.range N, km.stepsize i * (1 - km.stepsize i) * ‚ÄñT (km.x i) - km.x i‚Äñ ^ 2 ‚â§
      ‚Äñkm.x 0 - y0‚Äñ ^ 2 := by
      intro N
      refine (sum_bound N).trans ?_
      simp
  let a := fun n => ‚ÄñT (km.x n) - km.x n‚Äñ -- define a_n = ‚ÄñT x_n - x_n‚Äñ
  have a_noninc : ‚àÄ n, a (n + 1) ‚â§ a n := by
    intro n
    rcases km.hstepsize n with ‚ü®hs0, hs1‚ü©
    -- x_{n+1} - x_n = s_n ‚Ä¢ (T x_n - x_n)
    have hx : km.x (n + 1) - km.x n = km.stepsize n ‚Ä¢ (T (km.x n) - km.x n) := by
      rw [km.update n]; simp [ smul_sub]
    have eq : T (km.x (n + 1)) - km.x (n + 1) = (T (km.x (n + 1)) - T (km.x n)) + (1 - km.stepsize n) ‚Ä¢ (T (km.x n) - km.x n) := by
      calc
        T (km.x (n + 1)) - km.x (n + 1) = T (km.x (n + 1)) - T (km.x n) + T (km.x n) - km.x (n + 1) := by simp
        _ = T (km.x (n + 1)) - T (km.x n) + (1 - km.stepsize n) ‚Ä¢ (T (km.x n) - km.x n) := by
          nth_rw 2 [km.update n]
          simp only [smul_sub, sub_smul, one_smul]
          abel_nf
    calc
      a (n + 1) = ‚ÄñT (km.x (n + 1)) - km.x (n + 1)‚Äñ := rfl
      _ = ‚Äñ(T (km.x (n + 1)) - T (km.x n)) + (1 - km.stepsize n) ‚Ä¢ (T (km.x n) - km.x n)‚Äñ := by rw [eq]
      _ ‚â§ ‚ÄñT (km.x (n + 1)) - T (km.x n)‚Äñ + ‚Äñ(1 - km.stepsize n) ‚Ä¢ (T (km.x n) - km.x n)‚Äñ := by apply norm_add_le
      _ ‚â§ ‚Äñkm.x (n + 1) - km.x n‚Äñ + (1 - km.stepsize n) * ‚ÄñT (km.x n) - km.x n‚Äñ := by
        apply add_le_add
        ¬∑ exact (hT_nonexpansive (km.x (n + 1)) (km.x n))
        have h_nonneg : 0 ‚â§ 1 - km.stepsize n := by linarith
        -- prove ‚Äñ(1 - s) ‚Ä¢ v‚Äñ ‚â§ (1 - s) * ‚Äñv‚Äñ
        calc
          ‚Äñ(1 - km.stepsize n) ‚Ä¢ (T (km.x n) - km.x n)‚Äñ
              = ‚Äñ(1 - km.stepsize n)‚Äñ * ‚ÄñT (km.x n) - km.x n‚Äñ := by rw [norm_smul]
          _ = |1 - km.stepsize n| * ‚ÄñT (km.x n) - km.x n‚Äñ := by rw [Real.norm_eq_abs]
          _ = (1 - km.stepsize n) * ‚ÄñT (km.x n) - km.x n‚Äñ := by rw [abs_of_nonneg h_nonneg]
        linarith
      _= ‚Äñkm.stepsize n ‚Ä¢ (T (km.x n) - km.x n)‚Äñ + (1 - km.stepsize n) * ‚ÄñT (km.x n) - km.x n‚Äñ := by rw [hx]
      _= km.stepsize n * ‚ÄñT (km.x n) - km.x n‚Äñ + (1 - km.stepsize n) * ‚ÄñT (km.x n) - km.x n‚Äñ := by rw [norm_smul,Real.norm_eq_abs,abs_of_nonneg (hs0)]
      _= ‚ÄñT (km.x n) - km.x n‚Äñ := by ring
  rw [Converge_iff _ _]
  --Conduct a case-by-case analysis. If x0 = y0,trivial. Otherwise, use the method of contradiction.
  by_cases h_x0_eq_y0:  km.x 0 = y0
  ¬∑ intro Œµ Œµpos
    use 0
    intro n hn
    rcases hy0 with ‚ü®-, hyfix‚ü©
    rw[‚Üê h_x0_eq_y0] at hyfix
    have fixed_point: T (km.x n) - km.x n = 0 := by
      induction n with
      | zero => rw[sub_eq_zero]; exact hyfix
      | succ i ih => rw [km.update i];simp [ih _]
    rw[fixed_point];simpa
  --x0 ‚â† y0. Prove by contradiction: If a does not converge to 0, then there exists Œµ > 0 such that for any N, there is n ‚â• N with a n ‚â• Œµ
  by_contra! hnot
  rcases hnot with ‚ü®Œµ, Œµpos, hŒµ‚ü©
  have tend := km.hstepsize_sum
  -- The partial sum S is greater than 2*‚Äñx0 - y0‚Äñ^2 / Œµ^2 starting from some N0
  have tend_prop := (Filter.tendsto_atTop_atTop.mp tend) (2*‚Äñkm.x 0 - y0‚Äñ ^ 2 / Œµ^2)
  rcases tend_prop with ‚ü®N0, hN0‚ü©
  -- pick n0 ‚â• N0 and (a n0) ‚â• Œµ
  rcases (hŒµ N0) with ‚ü®n0, hn0_ge, hn0_ge_eps‚ü©
  -- For the partial sum up to n0 + 1, use the monotonicity a_i ‚â• a_{n0} (for i ‚â§ n0) to obtain a lower bound
  have lower : ‚àë i ‚àà Finset.range (n0 + 1), km.stepsize i * (1 - km.stepsize i) * (a i) ^ 2 ‚â•
      ‚àë i ‚àà Finset.range (n0 + 1), km.stepsize i * (1 - km.stepsize i)*Œµ ^ 2 := by
    apply Finset.sum_le_sum
    intro i hi
    have : i ‚â§ n0 := (Nat.lt_succ_iff.mp (Finset.mem_range.mp hi))
    have ai_ge : a i ‚â• a n0 := by
      exact (antitone_nat_of_succ_le a_noninc) (by omega)
    have ai_ge_eps : Œµ ‚â§ a i := by
      have h : Œµ ‚â§ ‚ÄñT (km.x n0) - km.x n0‚Äñ := by
        by_contra! H  -- H: ‚ÄñT (km.x n0) - km.x n0‚Äñ < Œµ
        rw[‚Üê zero_add Œµ ] at H
        exact hn0_ge_eps ‚ü®by linarith [norm_nonneg (T (km.x n0) - km.x n0)], H‚ü©
      linarith
    apply mul_le_mul_of_nonneg_left
    ¬∑ exact pow_le_pow_left‚ÇÄ (le_of_lt Œµpos) ai_ge_eps 2
    rcases km.hstepsize i with ‚ü®hs0, hs1‚ü©
    ¬∑ apply mul_nonneg
      ¬∑ exact hs0
      ¬∑ exact sub_nonneg.mpr hs1
  -- S ‚â• 2*‚Äñx0-y0‚Äñ^2 / Œµ^2
  have S_ge : ‚àë i ‚àà range (n0 + 1), km.stepsize i * (1 - km.stepsize i)
  ‚â• 2*‚Äñkm.x 0 - y0‚Äñ ^ 2 / Œµ^2:= by
    apply hN0
    exact le_trans (by linarith : N0 ‚â§ n0) (le_refl _)
  -- combine the upper and lower bounds to get a contradiction
  have lb: ‚àë i ‚àà range (n0 + 1), km.stepsize i * (1 - km.stepsize i) * (a i) ^ 2
  ‚â• (2* ‚Äñkm.x 0 - y0‚Äñ ^ 2 ) := by
    calc
      ‚àë i ‚àà range (n0 + 1), km.stepsize i * (1 - km.stepsize i) * (a i) ^ 2
          ‚â• ‚àë i ‚àà range (n0 + 1), km.stepsize i * (1 - km.stepsize i) * Œµ ^ 2 := by
            exact lower
      _ = Œµ ^ 2 *(‚àë i ‚àà range (n0 + 1), km.stepsize i * (1 - km.stepsize i))  := by
        have : (‚àë i ‚àà range (n0 + 1), km.stepsize i * (1 - km.stepsize i) * Œµ ^ 2) =
            ‚àë i ‚àà range (n0 + 1), Œµ ^ 2 * (km.stepsize i * (1 - km.stepsize i) ) := by
          apply Finset.sum_congr rfl
          intro i hi
          ring
        rw [this]
        -- Move Œµ^2 to the outside of the summation.
        rw [‚Üê @Finset.mul_sum ‚Ñï _ _ (range (n0 + 1))
        (fun i => km.stepsize i * (1 - km.stepsize i)) (Œµ ^ 2)]
      _ ‚â• 2*‚Äñkm.x 0 - y0‚Äñ ^ 2 := by
        have hpos : 0 ‚â§ Œµ ^ 2 := by exact pow_nonneg (le_of_lt Œµpos) 2
        calc
          Œµ ^ 2 * (‚àë i ‚àà Finset.range (n0 + 1), km.stepsize i * (1 - km.stepsize i))
          _ ‚â• Œµ ^ 2 * (2* ‚Äñkm.x 0 - y0‚Äñ ^ 2 / Œµ ^ 2) := by apply mul_le_mul_of_nonneg_left S_ge hpos
          _ = 2*‚Äñkm.x 0 - y0‚Äñ ^ 2 := by
            field_simp [ne_of_gt Œµpos]
  have ub := partial_le (n0 + 1)
  have mid: 2 * ‚Äñkm.x 0 - y0‚Äñ ^ 2 > ‚Äñkm.x 0 - y0‚Äñ ^ 2 := by
    refine lt_two_mul_self ?_
    have h_sub_ne : km.x 0 - y0 ‚â† 0 := by
      intro h
      apply h_x0_eq_y0
      rw[sub_eq_zero] at h
      exact h
    have h_norm_pos : 0 < ‚Äñkm.x 0 - y0‚Äñ := by
      apply norm_pos_iff.mpr
      exact h_sub_ne
    have : 0 < ‚Äñkm.x 0 - y0‚Äñ ^ 2 := pow_pos h_norm_pos (2)
    exact this
  linarith

/--
Sequence `x n` in KM algorithm converges weakly to a point `y0` in Fix T.
-/
lemma groetsch_theorem_iii [SeparableSpace H] [CompleteSpace H] {D : Set H}
(hD_convex : Convex ‚Ñù D) (hD_closed : IsClosed D) (T : H ‚Üí H) (h_Im_T_in_D : ‚àÄ x ‚àà D, T x ‚àà D)
(hT_nonexpansive : ‚àÄ x y, ‚ÄñT x - T y‚Äñ ‚â§ ‚Äñx - y‚Äñ) (km : KM D T) :
    ‚àÉ y0 ‚àà (FixOn T D), WeakConverge km.x y0 := by
  have h_fejer := (groetsch_theorem_i hD_convex hD_closed T h_Im_T_in_D hT_nonexpansive km)
  have h_x : ‚àÄ n, km.x n ‚àà D := by  --The proposition that D is a convex set is only used in the third conclusion.
    intro n                          --That is, conclusions (i) and (ii) do not require that D be a convex closed set.
    induction n with
    | zero =>  rw [km.initial_value];exact km.hx0
    | succ n ih =>
    have eq : km.x (n + 1) = (1 - km.stepsize n) ‚Ä¢ km.x n + km.stepsize n ‚Ä¢ (T (km.x n)) := by
      rw [km.update n]
      simp [smul_sub, sub_smul, one_smul]
      abel_nf
    have h1 : T (km.x n) ‚àà D := h_Im_T_in_D (km.x n) ih
    rcases km.hstepsize n with ‚ü®hs_nonneg, hs_le_one‚ü©
    have combo_in : (1 - km.stepsize n) ‚Ä¢ km.x n + km.stepsize n ‚Ä¢ T (km.x n) ‚àà D := by
      exact hD_convex (ih) h1 (sub_nonneg.mpr hs_le_one) (hs_nonneg) (sub_add_cancel _ _)
    rw [eq]
    exact combo_in
  --Prove that D is a sequentially weakly closed set --Theorem 3.34
  have h_D_seq_weak_closed : IsWeaklySeqClosed D := closed_is_weakly_seq_closed D hD_convex hD_closed
  have hT_nonexp : NonexpansiveOn T D := by
    intro x hx y hy
    simp only [edist_dist, ENNReal.coe_one, one_mul, dist_nonneg, ENNReal.ofReal_le_ofReal_iff]; rw [dist_eq_norm, dist_eq_norm]
    exact hT_nonexpansive x y
  have h_weak_cluster_in : ‚àÄ p : H, HasWeakSubseq p km.x ‚Üí p ‚àà (FixOn T D)  := by
    intro p h_cluster
    rcases h_cluster with ‚ü® œÜ, hœÜ , tend ‚ü©
    have p_in_D : p ‚àà D := by
      have : ‚àÄ n, (‚áë(toWeakSpace ‚Ñù H) ‚àò fun k ‚Ü¶ km.x (œÜ k)) n ‚àà ‚áë(toWeakSpace ‚Ñù H) '' D := by
        intro n
        simp only [comp_apply, Set.mem_image, EmbeddingLike.apply_eq_iff_eq, exists_eq_right]
        exact Set.mem_preimage.mp (h_x (œÜ n))
      obtain h := h_D_seq_weak_closed this tend
      exact inter_singleton_nonempty.mp (h_D_seq_weak_closed this tend)
    -- Prove that p is a fixpoint of T.
    have h_error_zero : Tendsto (fun n ‚Ü¶ km.x (œÜ n) - T (km.x (œÜ n))) atTop (ùìù 0):= by
      have h1 : Tendsto œÜ atTop atTop := StrictMono.tendsto_atTop hœÜ
      have h2 : Tendsto (fun n ‚Ü¶ km.x n - T (km.x n)) atTop (ùìù 0) := by
        -- ‚ÄñT (km.x n) - km.x n‚Äñ ‚Üí 0
        rw [tendsto_zero_iff_norm_tendsto_zero]
        have eq: Tendsto (fun n ‚Ü¶ ‚Äñkm.x n - T (km.x n)‚Äñ) atTop (ùìù 0) ‚Üî
          Tendsto (fun n ‚Ü¶ ‚ÄñT (km.x n)- km.x n‚Äñ) atTop (ùìù 0) := by
          apply tendsto_congr
          intro n
          rw [norm_sub_rev]
        rw[eq]
        exact (groetsch_theorem_ii hD_convex hD_closed T h_Im_T_in_D hT_nonexpansive km)
      exact Tendsto.comp h2 h1
    have D_nonempty: (D).Nonempty := by
      exact ‚ü® km.x0,km.hx0‚ü©
    have := corollary_4_28 hD_closed hD_convex D_nonempty hT_nonexp (fun n => km.x (œÜ n) ) (fun n => h_x (œÜ n) )
      p p_in_D tend h_error_zero
    exact ‚ü® p_in_D, this ‚ü©
  apply WeakConv_of_Fejermonotone_of_clusterpt_in (FixOn T D) (km.fix_T_nonempty) km.x h_fejer h_weak_cluster_in

/--
Formalization of Groetsch's theorem for Krasnosel'skii-Mann iteration
-/
theorem groetsch_theorem [SeparableSpace H] [CompleteSpace H] {D : Set H}
    (hD_convex : Convex ‚Ñù D) (hD_closed : IsClosed D) (T : H ‚Üí H) (h_Im_T_in_D : ‚àÄ x ‚àà D, T x ‚àà D)
    (hT_nonexpansive : ‚àÄ x y, ‚ÄñT x - T y‚Äñ ‚â§ ‚Äñx - y‚Äñ) (km : KM D T) :
    -- (i) Fej√©r monotonicity
    IsFejerMonotone km.x (FixOn T D)
    -- (ii) converges strongly to 0
    ‚àß(Tendsto (fun n ‚Ü¶ ‚ÄñT (km.x n) - km.x n‚Äñ)  atTop (ùìù 0))
    -- (iii) converges weakly to a fixpoint
    ‚àß‚àÉ y0 ‚àà (FixOn T D),WeakConverge km.x y0
    :=
      ‚ü®
        groetsch_theorem_i hD_convex hD_closed T h_Im_T_in_D hT_nonexpansive km,
        groetsch_theorem_ii hD_convex hD_closed T h_Im_T_in_D hT_nonexpansive km,
        groetsch_theorem_iii hD_convex hD_closed T h_Im_T_in_D hT_nonexpansive km
      ‚ü©
