/-
Copyright (c) 2025 Yifan Bai, Yantao Li. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yifan Bai, Yantao Li
-/
import Mathlib.Analysis.Normed.Module.WeakDual
import Mathlib.Analysis.InnerProductSpace.ProdL2
import Mathlib.Analysis.InnerProductSpace.Dual
import Mathlib.Analysis.Normed.Operator.BanachSteinhaus
import FormalizationFixpointIterations.InnerProductSpace.liminf

open Filter Metric Topology Function TopologicalSpace WeakBilin

variable {H : Type*} [NormedAddCommGroup H] [InnerProductSpace â„ H]
local notation "âŸª" aâ‚ ", " aâ‚‚ "âŸ«" => @inner â„ _ _ aâ‚ aâ‚‚

section topDualPairing

/--
The inner product is left continuous.
-/
def cont_inner_left (a : H) : H â†’L[â„] â„ where
  toFun := fun x => âŸªx, aâŸ«
  map_add' := by
    intro x y
    simp [inner_add_left]
  map_smul' := by
    intro c x
    simp [inner_smul_left]

/--
The flip of the topological dual pairing is injective.
-/
lemma topDualPairing_is_injective : Function.Injective â‡‘(topDualPairing â„ H).flip := by
  simp only [Function.Injective]
  intro a b hab
  have h1 : âŸªa, aâŸ« = âŸªb, aâŸ« := by
    change cont_inner_left a a = cont_inner_left a b
    rw [â† topDualPairing_apply, â† topDualPairing_apply, â† LinearMap.flip_apply]
    nth_rw 2 [â† LinearMap.flip_apply]; rw [â† hab]
  have h2 : âŸªa, bâŸ« = âŸªb, bâŸ« := by
    change cont_inner_left b a = cont_inner_left b b
    rw [â† topDualPairing_apply, â† topDualPairing_apply, â† LinearMap.flip_apply]
    nth_rw 2 [â† LinearMap.flip_apply]; rw [â† hab]
  have : a - b = 0 := by
    have h1': âŸªa - b, aâŸ« = 0 := calc
      _ = âŸªa ,aâŸ« - âŸªb, aâŸ« := by apply inner_sub_left a b a
      _ = âŸªa, aâŸ« - âŸªa, aâŸ« := by rw [h1]
      _ = 0 := by simp
    have h2': âŸªa - b, bâŸ« = 0 := calc
      _ = âŸªa, bâŸ« - âŸªb, bâŸ« := by apply inner_sub_left a b b
      _ = âŸªa, bâŸ« - âŸªa, bâŸ« := by rw [h2]
      _ = 0 := by simp
    apply (@inner_self_eq_zero â„ H _ _ _ (a - b)).1
    calc
      _ = âŸªa - b, aâŸ« - âŸªa - b, bâŸ« := inner_sub_right (a - b) a b
      _ = 0 - 0 := by rw [h1', h2']
      _ = 0 := by simp
  calc
    _ = a - b + b := Eq.symm (sub_add_cancel a b)
    _ = 0 + b := by rw [this]
    _ = b := by simp

theorem topDualPairing_eq (p : H) : âˆ€ y : H â†’L[â„] â„, (topDualPairing â„ H).flip p y = y p := by
  simp [LinearMap.flip_apply, topDualPairing_apply]

theorem topDualPairing_strong_dual [CompleteSpace H] (p : H) : âˆ€ y : H â†’L[â„] â„,
  (topDualPairing â„ H).flip p y = âŸª(InnerProductSpace.toDual â„ H).symm y, pâŸ« := by
  simp [LinearMap.flip_apply, topDualPairing_apply]

theorem topDualPairing_eq_inner [CompleteSpace H] (x y : H) :
  (topDualPairing â„ H).flip x ((cont_inner_left y)) = âŸªx, yâŸ« := by
  rw [topDualPairing_eq]; simp [cont_inner_left]

theorem topDualPairing_strong_dual_seq [CompleteSpace H] (x : â„• â†’ H) : âˆ€ y : H â†’L[â„] â„,
  (fun n â†¦ ((topDualPairing â„ H).flip (x n)) y) =
  fun n => âŸª(InnerProductSpace.toDual â„ H).symm y, x nâŸ« := by
  intro y; ext n
  exact topDualPairing_strong_dual (x n) y

end topDualPairing

section WeakConverge

/--
Definition: Weak convergence in an inner product space.
-/
def WeakConverge (x : â„• â†’ H) (p : H) :=
  Tendsto ((toWeakSpace â„ H) âˆ˜ x) atTop (nhds ((toWeakSpace â„ H) p))

theorem weakConverge_iff_inner_converge_pre (x : â„• â†’ H) (p : H) : WeakConverge x p â†”
  âˆ€ y : H â†’L[â„] â„, Tendsto (fun n â†¦ (topDualPairing â„ H).flip (x n) y)
    atTop (nhds ((topDualPairing â„ H).flip p y)) := by
  simp only [WeakConverge]
  apply tendsto_iff_forall_eval_tendsto
  exact topDualPairing_is_injective

lemma ddd : (topDualPairing â„ H).flip = (ContinuousLinearMap.coeLM â„).flip := rfl

theorem weakConverge_iff_inner_converge [CompleteSpace H] (x : â„• â†’ H) (p : H) : WeakConverge x p â†”
  âˆ€ y : H, Tendsto (fun n â†¦ âŸªx n, yâŸ«) atTop (nhds âŸªp, yâŸ«) := by
  constructor
  Â· intro h y
    rw [weakConverge_iff_inner_converge_pre] at h
    specialize h (cont_inner_left y)
    simp only [cont_inner_left] at h
    exact h
  intro h
  rw [weakConverge_iff_inner_converge_pre]
  intro y
  let yf := (InnerProductSpace.toDual â„ H).symm y
  rw [topDualPairing_strong_dual, topDualPairing_strong_dual_seq]
  have : (fun n â†¦ inner â„ ((InnerProductSpace.toDual â„ H).symm y) (x n)) =
    (fun n â†¦ inner â„  (x n) ((InnerProductSpace.toDual â„ H).symm y)) := by
    ext n; rw [real_inner_comm]
  rw [real_inner_comm, this]; apply h

omit [InnerProductSpace â„ H] in
lemma tendsto_iff_sub_tendsto_zero {G : Type*} [NormedAddCommGroup G]
  (x : â„• â†’ G) (p : G) : Tendsto x atTop (nhds p)
  â†” Tendsto (fun n â†¦ x n - p) atTop (nhds 0) := Iff.symm tendsto_sub_nhds_zero_iff

lemma tendsto_iff_sub_tendsto_zero_inner (x : â„• â†’ H) (p : H) (y : H) :
  Tendsto (fun n â†¦ âŸªx n, yâŸ«) atTop (nhds âŸªp, yâŸ«)
  â†” Tendsto (fun n â†¦ âŸªx n - p, yâŸ«) atTop (nhds 0) := by
  have hfun (y : H): (fun n â†¦ âŸªx n - p, yâŸ«) = (fun n â†¦ âŸªx n, yâŸ« - âŸªp, yâŸ«) := by
    ext n; simp [inner_sub_left]
  rw [hfun y]
  constructor
  Â· exact fun h => (tendsto_iff_sub_tendsto_zero (fun n â†¦ âŸªx n, yâŸ«) âŸªp, yâŸ«).1 h
  exact fun h => (tendsto_iff_sub_tendsto_zero (fun n â†¦ âŸªx n, yâŸ«) âŸªp, yâŸ«).2 h

theorem weakConverge_iff_inner_converge' [CompleteSpace H] (x : â„• â†’ H) (p : H) :
  WeakConverge x p â†” âˆ€ y : H, Tendsto (fun n â†¦ âŸªx n - p, yâŸ«) atTop (nhds 0) := by
  constructor
  Â· intro h y
    refine (tendsto_iff_sub_tendsto_zero_inner x p y).mp ?_
    apply (weakConverge_iff_inner_converge x p).1 h
  intro h; rw [weakConverge_iff_inner_converge]; intro y
  specialize h y; exact (tendsto_iff_sub_tendsto_zero_inner x p y).mpr h


-- Left hand side in proof of Lemma 2.42
theorem lim_inner_seq_eq_norm [CompleteSpace H] (x : â„• â†’ H) (p : H) (h : WeakConverge x p) :
  Tendsto (fun n => âŸªx n, pâŸ«) atTop (nhds (â€–pâ€–^2)) := by
  obtain hw := (weakConverge_iff_inner_converge' x p).1 h p
  rw [â† tendsto_iff_sub_tendsto_zero_inner] at hw
  rwa [real_inner_self_eq_norm_sq p] at hw

end WeakConverge

section FiniteDimensional

omit [InnerProductSpace â„ H] in
theorem seq_converge_iff_norm_converge {G : Type*} [NormedAddCommGroup G] (x : â„• â†’ G) (p : G) :
  Tendsto x atTop (nhds p) â†” Tendsto (fun n => â€–x n - pâ€–^2) atTop (nhds 0) := by
  constructor
  Â· intro h
    rw [tendsto_iff_sub_tendsto_zero] at h
    rw [Metric.tendsto_atTop]
    intro Îµ hÎµ
    rw [Metric.tendsto_atTop] at h
    obtain âŸ¨N, hNâŸ© := h (Real.sqrt Îµ) (Real.sqrt_pos.mpr hÎµ)
    use N
    intro n hn
    specialize hN n hn
    simp only [dist] at *
    simp only [sub_zero, abs_pow, abs_norm]
    refine Real.sq_lt.mpr ?_
    constructor
    Â· have nonneg : 0 â‰¤ â€–x n - pâ€– := by
        exact norm_nonneg (x n - p)
      have lt: -âˆšÎµ < 0 := by
        simp
        linarith
      exact lt_of_le_of_lt' nonneg lt
    exact mem_ball_zero_iff.mp hN
  intro h
  rw [tendsto_iff_sub_tendsto_zero]
  rw [Metric.tendsto_atTop]
  intro Îµ hÎµ
  rw [Metric.tendsto_atTop] at h
  obtain âŸ¨N, hNâŸ© := h (Îµ ^ 2) (sq_pos_of_pos hÎµ)
  use N
  intro n hn
  specialize hN n hn
  simp only [dist] at *
  simp only [sub_zero, abs_pow, abs_norm] at hN
  apply Real.sq_lt.mp at hN
  rcases hN with âŸ¨h1, h2âŸ©
  have:âˆš(Îµ ^ 2) = Îµ := by
    rw [Real.sqrt_sq hÎµ.le]
  rw [this] at h2
  -- simp at h2
  have : â€–x n - pâ€– = dist (x n - p) 0 := by
    exact Eq.symm (dist_zero_right (x n - p))
  rw [â† this]
  exact h2

omit [NormedAddCommGroup H] [InnerProductSpace â„ H] in
theorem tsum_tendsto_zero (w : Finset H) (f : {x//x âˆˆ w} â†’ â„• â†’ â„)
  (h : âˆ€ i : {x//x âˆˆ w}, Tendsto (f i) atTop (nhds 0)):
  Tendsto (fun n => âˆ‘ i : {x//x âˆˆ w}, f i n) atTop (nhds 0) := by
  have h_sum : Tendsto (fun n => âˆ‘ i : {x//x âˆˆ w}, f i n) atTop
    (nhds (âˆ‘ i : {x//x âˆˆ w}, (0 : â„))) := by
    apply tendsto_finset_sum; exact fun i a â†¦ h i
  simp only [Finset.sum_const_zero] at h_sum; exact h_sum

theorem tendsto_norm_congr (x : â„• â†’ â„) (h : Tendsto x atTop (nhds 0)) :
  Tendsto (fun n => â€–x nâ€–^2) atTop (nhds 0) := by
  rw[â† sub_zero x]; exact (seq_converge_iff_norm_converge x 0).mp h

theorem finite_weak_converge_strong_converge [FiniteDimensional â„ H] (x : â„• â†’ H) (p : H)
  (h : WeakConverge x p) : Tendsto x atTop (nhds p) := by
  apply (seq_converge_iff_norm_converge x p).2
  simp only [WeakConverge] at h
  obtain âŸ¨w,b,hbâŸ© := exists_orthonormalBasis â„ H
  have (n:â„•) := OrthonormalBasis.sum_sq_norm_inner_left b (x n - p)
  have hfuneq: (fun n â†¦ â€–x n - pâ€– ^ 2) = fun n => âˆ‘ i : {x//x âˆˆ w},
    â€–inner â„ (x n - p) (b i)â€– ^ 2 := by
    ext n; symm; exact this n
  rw [hfuneq]
  apply tsum_tendsto_zero w (fun i:{x//x âˆˆ w} => (fun n => â€–inner â„ (x n - p) (b i)â€– ^ 2))
  intro i; apply tendsto_norm_congr; apply (weakConverge_iff_inner_converge' x p).1; exact h

theorem strong_converge_weak_converge [CompleteSpace H] (x : â„• â†’ H) (p : H)
  (h : Tendsto x atTop (nhds p)) : WeakConverge x p := by
  rw [weakConverge_iff_inner_converge]
  intro y
  have hy : Tendsto (fun _ : â„• => y) atTop (nhds y) := tendsto_const_nhds
  simpa using (Filter.Tendsto.inner (ğ•œ:=â„) (E:=H) h hy)

end FiniteDimensional


section WeakConvergeBounded

-- def fn_norm : â„• â†’ H â†’L[â„] â„ := fun n =>
-- def xn_inner (p : H) : H â†’â‚›â‚—[â„] â„ where
--   toFun := fun z => âŸªp, zâŸ«
--   map_add' := fun u v => inner_add_right p u v
--   map_smul' := fun c u => inner_smul_right p u c

/--
Theorem: Weakly convergent sequence is bounded.
-/
theorem weakly_converge_norm_bounded [CompleteSpace H] (x : â„• â†’ H) (p : H)
  (h_wkconv_x : WeakConverge x p) : âˆƒ M, âˆ€ n, â€–x nâ€– â‰¤ M := by
  let f : â„• â†’ H â†’L[â„] â„ := fun n =>
    LinearMap.mkContinuous
      { toFun := fun z => âŸªx n, zâŸ«
        map_add' := fun u v => inner_add_right (x n) u v
        map_smul' := fun c u => inner_smul_right (x n) u c}
      â€–x nâ€–
      fun z => by
        simp only [LinearMap.coe_mk, AddHom.coe_mk, Real.norm_eq_abs]
        exact abs_real_inner_le_norm (x n) z
  have h_f_n_y_upbd : âˆ€ y : H, âˆƒ N : â„•, âˆƒ M : â„, âˆ€ n â‰¥ N, |f n y| â‰¤ M := by
    intro y; rw [weakConverge_iff_inner_converge] at h_wkconv_x
    specialize h_wkconv_x y; rw [Metric.tendsto_atTop] at h_wkconv_x
    specialize h_wkconv_x (1) (one_pos)
    obtain âŸ¨N, hNâŸ© := h_wkconv_x
    use N, |âŸªp, yâŸ«| + 1
    intro n hn; specialize hN n hn; simp only [f]
    simp only [LinearMap.mkContinuous_apply, LinearMap.coe_mk, AddHom.coe_mk]
    rw [Real.dist_eq] at hN
    have : |inner â„ (x n) y| - |inner â„ p y| < 1 := by
      calc
        _ â‰¤ |inner â„ (x n) y - inner â„ p y| := by apply abs_sub_abs_le_abs_sub
        _ < 1 := hN
    linarith
  have h_f_n_y_pointwise_bounded : âˆ€ y : H, âˆƒ M : â„, âˆ€ n : â„•, |f n y| â‰¤ M := by
    intro y; specialize h_f_n_y_upbd y; obtain âŸ¨N, hNâŸ© := h_f_n_y_upbd
    by_cases N_zero : N = 0
    Â· rw [N_zero] at hN; rcases hN with âŸ¨M, hMâŸ©; use M; intro n; exact hM n (Nat.zero_le n)
    Â· let M0 := (Finset.range N).sup' âŸ¨0, Finset.mem_range.mpr
        (Nat.pos_of_ne_zero â€¹N â‰  0â€º)âŸ© (fun n => |(f n) y|)
      have ha : âˆ€ a âˆˆ Finset.range N, |(f a) y| â‰¤ M0 := by
        intro a ha;simp only [M0, Finset.le_sup'_iff, Finset.mem_range]; use a
        constructor
        Â· exact List.mem_range.mp ha
        Â· simp
      rcases hN with âŸ¨M1, hM1âŸ©; use max M0 M1; intro n
      by_cases hn : n < N
      Â· calc
          _ â‰¤ M0 := by apply ha n; exact Finset.mem_range.mpr hn
          _ â‰¤ max M0 M1 := by apply le_max_left
      Â· push_neg at hn
        calc
          _ â‰¤ M1 := by apply hM1; exact hn
          _ â‰¤ max M0 M1 := by apply le_max_right
  have h_norm_sup_t_n_y : âˆ€ y : H, âˆƒ M : â„, â¨† n : â„•, |f n y| â‰¤ M := by
    intro y; rcases h_f_n_y_pointwise_bounded y with âŸ¨M, hMâŸ©; use M; exact ciSup_le hM
  have h_f_bounded : âˆƒ C, âˆ€ n, â€–f nâ€– â‰¤ C := by
    have h_pointwise : âˆ€ y, âˆƒ M, âˆ€ n, |f n y| â‰¤ M := by intro y; exact h_f_n_y_pointwise_bounded y
    exact banach_steinhaus h_pointwise
  obtain âŸ¨C, hCâŸ© := h_f_bounded; use C; intro n
  have h_norm_eq : â€–f nâ€– = â€–x nâ€– := by
    refine ContinuousLinearMap.opNorm_eq_of_bounds ?_ ?_ ?_
    Â· simp
    Â· intro z; simp only [Real.norm_eq_abs]; exact abs_real_inner_le_norm (x n) z
    Â· intro M hM h; simp only [Real.norm_eq_abs] at h; specialize h (x n)
      rw [abs_of_nonneg] at h
      Â· have : â€–x nâ€– â‰¥ 0 := norm_nonneg (x n)
        by_cases h1: â€–x nâ€– = 0
        Â· rw [h1]; assumption
        Â· push_neg at h1
          have : â€–x nâ€– > 0 := by
            apply lt_of_le_of_ne
            Â· exact this
            Â· intro h2; rw [h2] at h1; contradiction
          simp only [LinearMap.mkContinuous_apply, LinearMap.coe_mk, AddHom.coe_mk,
            inner_self_eq_norm_sq_to_K, RCLike.ofReal_real_eq_id, id_eq, f] at h
          rw [pow_two] at h
          exact le_of_mul_le_mul_right h this
      Â· exact real_inner_self_nonneg
  rw [â† h_norm_eq]; exact hC n

end WeakConvergeBounded

/--
Theorem: norm is weakly lsc. (Lemma 2.42)
-/
theorem norm_weakly_lsc [CompleteSpace H] (x : â„• â†’ H) (p : H) (h : WeakConverge x p) :
  Real.toEReal â€–pâ€– â‰¤ liminf (fun n => Real.toEReal â€–x nâ€–) atTop := by
  let x' := fun ( n : â„• ) => âŸªx n, pâŸ«
  let y' := fun ( n : â„• ) => â€–x nâ€– * â€–pâ€–
  have hxy : âˆ€ n, x' n â‰¤ y' n := by
    intro n; exact real_inner_le_norm (x n) p
  have h1 : Tendsto x' atTop (nhds (â€–pâ€– ^ 2)) := lim_inner_seq_eq_norm x p h
  have nonneg1 : Real.toEReal â€–pâ€– â‰¥ 0 := EReal.coe_nonneg.mpr (norm_nonneg p)
  have nonneg2 : âˆ€ n, Real.toEReal â€–x nâ€– â‰¥ 0 := fun n â†¦ EReal.coe_nonneg.mpr (norm_nonneg (x n))
  by_cases hp1 : Real.toEReal â€–pâ€– = 0
  Â· simp only [hp1]
    calc
      _ = liminf (fun n â†¦ (0 : EReal)) atTop := by
        symm; apply @Filter.liminf_const EReal â„• _ atTop _ (Real.toEReal 0)
      _ â‰¤ liminf (fun n â†¦ Real.toEReal â€–x nâ€–) atTop := by
        apply liminf_le_liminf
        Â· apply Eventually.of_forall
          intro n
          simp
        Â· simp only [ge_iff_le]
          use 0
          refine eventually_const.mpr ?_
          simp
        Â· simp only [autoParam]
          apply Filter.IsBoundedUnder.isCoboundedUnder_ge
          simp only [IsBoundedUnder, IsBounded]
          have h_norm_bounded : âˆƒ M, âˆ€ n, â€–x nâ€– â‰¤ M :=
            weakly_converge_norm_bounded x p h
          obtain âŸ¨M, hMâŸ© := h_norm_bounded
          use M
          refine eventually_map.mp ?_
          simp only [Set.mem_setOf_eq, eventually_map, EReal.coe_le_coe_iff, eventually_atTop,
            ge_iff_le]
          use 0
          intro n hn
          exact hM n
  Â· have hp2 : Real.toEReal â€–pâ€– â‰  âŠ¥ := by simp
    have hp3 : Real.toEReal â€–pâ€– â‰  âŠ¤ := by simp
    push_neg at hp1
    have h_lim : Real.toEReal (â€–pâ€– ^ 2) â‰¤ liminf (fun n => Real.toEReal (y' n)) atTop :=
      EReal.limit_le_liminf x' y' (â€–pâ€– ^ 2) h1 hxy
    simp only [EReal.coe_pow, EReal.coe_mul, y'] at h_lim
    have h2 : liminf (fun n â†¦ Real.toEReal â€–x nâ€– * Real.toEReal â€–pâ€–) atTop
      = (liminf (fun n â†¦ Real.toEReal â€–x nâ€–) atTop) * Real.toEReal â€–pâ€– := EReal.liminf_mul_const x p
    rw [h2] at h_lim
    have p_norm_eq : Real.toEReal (â€–pâ€– * â€–pâ€–)  = Real.toEReal â€–pâ€– * Real.toEReal â€–pâ€– := by
      rw [â† EReal.coe_mul]
    have eq: â€–pâ€–^2 = â€–pâ€– * â€–pâ€– := by linarith
    have eq': Real.toEReal (â€–pâ€– ^ 2) = Real.toEReal â€–pâ€– * Real.toEReal â€–pâ€– := by
      rw [eq, p_norm_eq]
    have : Real.toEReal â€–pâ€– * Real.toEReal â€–pâ€–
      â‰¤ liminf (fun n â†¦ Real.toEReal â€–x nâ€–) atTop * Real.toEReal â€–pâ€– := by
      calc
        _ = Real.toEReal (â€–pâ€– ^ 2) := by rw [eq']
        _ â‰¤ liminf (fun n => Real.toEReal (y' n)) atTop := by convert h_lim
        _ = liminf (fun n => Real.toEReal (â€–x nâ€– * â€–pâ€–)) atTop := by simp [y']
        _ = liminf (fun n => Real.toEReal â€–x nâ€– * Real.toEReal â€–pâ€– ) atTop := by congr
        _ = liminf (fun n â†¦ Real.toEReal â€–x nâ€–) atTop * Real.toEReal â€–pâ€– := by rw [â† h2]
    calc
      _ = Real.toEReal â€–pâ€– / Real.toEReal â€–pâ€– * Real.toEReal â€–pâ€– := by
        symm; apply EReal.div_mul_cancel hp2 hp3 hp1
      _ = Real.toEReal â€–pâ€– * Real.toEReal â€–pâ€– / Real.toEReal â€–pâ€– := by apply EReal.mul_div_right
      _ â‰¤ liminf (fun n â†¦ â†‘â€–x nâ€–) atTop * Real.toEReal â€–pâ€– / Real.toEReal â€–pâ€– := by
        apply EReal.div_le_div_right_of_nonneg nonneg1 this
      _ = liminf (fun n â†¦ â†‘â€–x nâ€–) atTop / Real.toEReal â€–pâ€– * Real.toEReal â€–pâ€– := by
        symm; apply EReal.mul_div_right
      _ = liminf (fun n â†¦ â†‘â€–x nâ€–) atTop := by apply EReal.div_mul_cancel hp2 hp3 hp1

/--
Lemma 2.51 (i) : ``Tendsto x atTop (nhds p)`` if and only if `WeakConverge x p` and
`limsup â€–x nâ€– â‰¤ â€–pâ€–`.
-/
theorem weak_converge_limsup_le_iff_strong_converge [CompleteSpace H] (x : â„• â†’ H) (p : H) :
  WeakConverge x p âˆ§ limsup (fun n => Real.toEReal â€–x nâ€–) atTop â‰¤ Real.toEReal â€–pâ€– â†”
  Tendsto x atTop (nhds p) := by
  by_cases upper_bound : Â¬ (âˆƒ M : â„, âˆ€ n, â€–x nâ€– â‰¤ M)
  Â· push_neg at upper_bound
    constructor
    Â· rintro âŸ¨hweak, hlimsupâŸ©; exfalso
      have hlimsup_top : limsup (fun n => Real.toEReal â€–x nâ€–) atTop = âŠ¤ := by
        simp only [limsup, limsSup, eventually_map, eventually_atTop, ge_iff_le, sInf_eq_top,
          Set.mem_setOf_eq, forall_exists_index]
        intro a N hb
        by_contra ha_ne_top
        push_neg at ha_ne_top
        by_cases ha_ne_bot : a = âŠ¥
        Â· simp only [ha_ne_bot, le_bot_iff, EReal.coe_ne_bot, imp_false, not_le] at hb
          specialize hb N
          simp at hb
        push_neg at ha_ne_bot
        lift a to â„ using âŸ¨ha_ne_top, ha_ne_botâŸ© with a0
        by_cases hN : N = 0
        Â· simp only [hN, zero_le, EReal.coe_le_coe_iff, forall_const] at hb
          obtain âŸ¨m, hmâŸ© := upper_bound (a0 + 1)
          have : â€–x mâ€– â‰¤ a0 := by
            specialize hb m; assumption
          linarith
        Â· push_neg at hN
          let M1 := Finset.sup' (Finset.range N) (by simp [hN]) (fun k => â€–x kâ€–)
          let M := max M1 a0
          have hall : âˆ€ n, â€–x nâ€– â‰¤ M := by
            intro n; by_cases hn : n < N
            Â· have : â€–x nâ€– â‰¤ M1 := by
                apply Finset.le_sup'_of_le
                Â· simp only [Finset.mem_range]; exact hn
                Â· exact le_rfl
              exact le_trans this (le_max_left M1 a0)
            Â· push_neg at hn
              have : Real.toEReal â€–x nâ€– â‰¤ Real.toEReal a0 := hb n hn
              rw [EReal.coe_le_coe_iff] at this; exact le_trans this (le_max_right M1 a0)
          obtain âŸ¨m, hmâŸ© := upper_bound (M + 1); specialize hall m; linarith
      rw [hlimsup_top] at hlimsup; simp at hlimsup
    intro h
    constructor
    Â· exact strong_converge_weak_converge x p h
    rw[Metric.tendsto_atTop] at h; exfalso; specialize h 1 zero_lt_one
    obtain âŸ¨N, hNâŸ© := h
    let x0 := Finset.sup' (Finset.range (N + 1)) (by simp) (fun n â†¦ â€–x nâ€–)
    let M := max (x0 + 1) (â€–pâ€– + 1)
    obtain âŸ¨n, hnâŸ© := upper_bound M
    have hn_ge : n â‰¥ N := by
      classical
      by_contra hlt
      have hx0_le : â€–x nâ€– â‰¤ x0 := by
        have hmem : n âˆˆ Finset.range (N + 1) := by
          have : n < N + 1 := by
            apply Nat.lt_succ_of_lt; push_neg at hlt; exact hlt
          simpa [Finset.mem_range] using this
        exact Finset.le_sup'_of_le (fun k â†¦ â€–x kâ€–) hmem (le_rfl)
      have hcontr : â€–x nâ€– â‰¤ M := by
        calc
          _ â‰¤ x0 + 1 := by linarith
          _ â‰¤ M := by apply le_max_left
      exact not_lt_of_ge hcontr hn
    have hdist : dist (x n) p > 1 := by
      have hnorm : â€–x nâ€– > â€–pâ€– + 1 := lt_of_le_of_lt (le_max_right _ _) hn
      have hbound : â€–x n - pâ€– â‰¥ â€–x nâ€– - â€–pâ€– := norm_sub_norm_le (x n) p
      have h1: â€–x nâ€– - â€–pâ€– > 1 := by linarith
      simp only [dist_eq_norm, gt_iff_lt]
      exact lt_of_lt_of_le h1 hbound
    have hdist' : dist (x n) p â‰¥ 1 := hdist.le
    have : dist (x n) p < 1 := hN n hn_ge
    exact (not_lt_of_ge hdist') this
  have h: liminf (fun n => Real.toEReal â€–x nâ€–) atTop
    â‰¤ limsup (fun n => Real.toEReal â€–x nâ€–) atTop := by
    push_neg at upper_bound; apply liminf_le_limsup
    Â· obtain âŸ¨M, hMâŸ© := upper_bound
      have hbounded : IsBoundedUnder (Â· â‰¤ Â·) atTop (fun n â†¦ Real.toEReal â€–x nâ€–) := by
        refine âŸ¨M, ?_âŸ©
        have : âˆ€á¶  n in atTop, â€–x nâ€– â‰¤ M := by exact Eventually.of_forall hM
        simpa using this
      exact hbounded
    have hbounded : IsBoundedUnder (Â· â‰¥ Â·) atTop (fun n â†¦ Real.toEReal â€–x nâ€–) := by
      refine âŸ¨0, ?_âŸ©
      have : âˆ€á¶  n in atTop, 0 â‰¤ â€–x nâ€– := Eventually.of_forall (by intro n; exact norm_nonneg (x n))
      simp
    exact hbounded
  push_neg at upper_bound
  constructor
  Â· rintro âŸ¨hweak, hlimsupâŸ©
    have h' :Real.toEReal â€–pâ€– â‰¤ liminf (fun n => Real.toEReal â€–x nâ€–) atTop := by
      apply norm_weakly_lsc; exact hweak
    have eq: limsup (fun n â†¦ Real.toEReal â€–x nâ€–) atTop =
      liminf (fun n â†¦ Real.toEReal â€–x nâ€–) atTop:= by
      apply le_antisymm
      Â· calc
          _ â‰¤ Real.toEReal â€–pâ€– := hlimsup
          _ â‰¤ liminf (fun n => Real.toEReal â€–x nâ€–) atTop := h'
      Â· exact h
    have hlim : Tendsto (fun n => â€–x nâ€–) atTop (nhds â€–pâ€–) := by
      apply EReal.tendsto_coe.mp; apply tendsto_of_liminf_eq_limsup
      Â· rw [eq] at hlimsup
        apply le_antisymm
        Â· exact hlimsup
        exact h'
      Â· rw [â† eq] at h'
        apply le_antisymm
        Â· exact hlimsup
        Â· exact h'
      Â· obtain âŸ¨M, hMâŸ© := upper_bound
        have hbounded : IsBoundedUnder (Â· â‰¤ Â·) atTop (fun n â†¦ Real.toEReal â€–x nâ€–) := by
          refine âŸ¨M, ?_âŸ©
          have : âˆ€á¶  n in atTop, â€–x nâ€– â‰¤ M := by exact Eventually.of_forall hM
          simpa using this
        exact hbounded
      have hbounded : IsBoundedUnder (Â· â‰¥ Â·) atTop (fun n â†¦ Real.toEReal â€–x nâ€–) := by
        refine âŸ¨0, ?_âŸ©
        have : âˆ€á¶  n in atTop, 0 â‰¤ â€–x nâ€– :=
          Eventually.of_forall (by intro n; exact norm_nonneg (x n))
        simp
      exact hbounded
    have hnorm : Tendsto (fun n => â€–x nâ€–) atTop (nhds â€–pâ€–) := by simpa using hlim
    have hsub : Tendsto (fun n => x n - p) atTop (nhds 0) := by
      apply (tendsto_iff_sub_tendsto_zero x p).1
      apply (seq_converge_iff_norm_converge x p).2
      have eq2:âˆ€ n, â€–x n - pâ€– ^2 = â€–x nâ€–^2 - 2 * âŸªx n, pâŸ« + â€–pâ€–^2 := by
        intro n; rw [â† @norm_sub_sq_real]
      simp only [eq2]
      have h1 : Tendsto (fun n => â€–x nâ€–^2) atTop (nhds (â€–pâ€–^2)) := by
        simpa [pow_two] using hnorm.mul hnorm
      have h2 : Tendsto (fun n => 2 * âŸªx n, pâŸ«) atTop (nhds (2 * â€–pâ€–^2)) := by
        have : Tendsto (fun n => âŸªx n, pâŸ«) atTop (nhds (â€–pâ€–^2)) := lim_inner_seq_eq_norm x p hweak
        simpa using (tendsto_const_nhds (x := (2:â„))).mul this
      have h3 : Tendsto (fun n => â€–pâ€–^2) atTop (nhds (â€–pâ€–^2)) := tendsto_const_nhds (Î± := â„•)
      convert h1.sub h2 |>.add h3 using 2; ring
    have hnorm_sq : Tendsto (fun n => â€–x n - pâ€– ^ 2) atTop (nhds 0) := by
      have hnorm : Tendsto (fun n => â€–x n - pâ€–) atTop (nhds 0) :=
        tendsto_zero_iff_norm_tendsto_zero.mp hsub
      simpa [pow_two] using hnorm.mul hnorm
    exact (seq_converge_iff_norm_converge x p).2 hnorm_sq
  intro h'
  constructor
  Â· exact strong_converge_weak_converge x p h'
  have hnorm : Tendsto (fun n => â€–x nâ€–) atTop (nhds â€–pâ€–) := Tendsto.norm h'
  have hnorm_ereal : Tendsto (fun n => Real.toEReal â€–x nâ€–) atTop (nhds (Real.toEReal â€–pâ€–)) := by
    exact EReal.tendsto_coe.mpr hnorm
  have hlimsup : limsup (fun n => Real.toEReal â€–x nâ€–) atTop = Real.toEReal â€–pâ€– := by
    obtain âŸ¨M, hMâŸ© := upper_bound
    have hbdd_above : IsBoundedUnder (Â· â‰¤ Â·) atTop (fun n â†¦ Real.toEReal â€–x nâ€–) := by
      refine âŸ¨M, ((Eventually.of_forall hM).mono (by intro n hn; simpa))âŸ©
    have hbdd_below : IsBoundedUnder (Â· â‰¥ Â·) atTop (fun n â†¦ Real.toEReal â€–x nâ€–) := by
      refine âŸ¨0, ?_âŸ©; apply Eventually.of_forall (fun (n : â„•) => ?_); simp
    apply Tendsto.limsup_eq; exact hnorm_ereal
  rw [hlimsup]

/--
Corollary 2.52 : `Tendsto x atTop (nhds p)` if and only if `WeakConverge x p` and
`Tendsto (fun n => â€–x nâ€–) atTop (nhds â€–pâ€–)`.
-/
theorem strong_converge_iff_weak_norm_converge [CompleteSpace H] (x : â„• â†’ H) (p : H) :
  Tendsto x atTop (nhds p) â†”
  WeakConverge x p âˆ§ Tendsto (fun n => â€–x nâ€–) atTop (nhds â€–pâ€–) := by
  constructor
  Â· intro h
    constructor
    Â· exact strong_converge_weak_converge x p h
    exact Tendsto.norm h
  intro âŸ¨h1, h2âŸ©; apply (seq_converge_iff_norm_converge x p).2
  have norm_expand : âˆ€ n, â€–x n - pâ€–^2 = â€–x nâ€–^2 - 2 * âŸªx n, pâŸ« + â€–pâ€–^2 := by
    intro n; rw [â† @norm_sub_sq_real]
  simp only [norm_expand]
  have hnorm_sq : Tendsto (fun n => â€–x nâ€–^2) atTop (nhds (â€–pâ€–^2)) := by
    simpa [pow_two] using h2.mul h2
  have hinner : Tendsto (fun n => 2 * âŸªx n, pâŸ«) atTop (nhds (2 * â€–pâ€–^2)) := by
    have : Tendsto (fun n => âŸªx n, pâŸ«) atTop (nhds (â€–pâ€–^2)) := lim_inner_seq_eq_norm x p h1
    simpa using (tendsto_const_nhds (x := (2:â„))).mul this
  have hconst : Tendsto (fun n => â€–pâ€–^2) atTop (nhds (â€–pâ€–^2)) := tendsto_const_nhds (Î± := â„•)
  convert hnorm_sq.sub hinner |>.add hconst using 2; ring

/--
For a weakly convergent sequence `x n` converging to `x_lim` and a strongly convergent
sequence `u n` converging to `u_lim`, the inner product sequence `inner â„ (x n) (u n)`
converges to `inner â„ x_lim u_lim`.
-/
theorem mix_convergence [CompleteSpace H]
  {x : â„• â†’ H} {x_lim : H} {u : â„• â†’ H} {u_lim : H} {h_wkconv_x : WeakConverge x x_lim}
  {h_conv_u : Tendsto u atTop (ğ“ u_lim)}
  : Tendsto (fun n => inner â„ (x n) (u n)) atTop (ğ“ (inner â„ x_lim u_lim)) := by
  have eq : (fun n => inner â„ (x n) (u n) - inner â„ x_lim u_lim) =
    (fun n => inner â„ (x n) (u n - u_lim)) + (fun n => inner â„ (x n - x_lim) u_lim) := by
      funext n; simp [inner_sub_left, inner_sub_right]
  have âŸ¨M, hMâŸ© : âˆƒ M, âˆ€ n, â€–x nâ€– â‰¤ M :=
    weakly_converge_norm_bounded x x_lim h_wkconv_x
  have h1: Tendsto (fun n => inner â„ (x n) (u n - u_lim)) atTop (ğ“ 0) := by
    have h_u_diff : Tendsto (fun n => u n - u_lim) atTop (ğ“ 0) :=
      (tendsto_iff_sub_tendsto_zero u u_lim).mp h_conv_u
    by_cases M_zero : M = 0
    Â· have h_xn_zero : âˆ€ n, x n = 0 := by
        intro n; specialize hM n
        have : â€–x nâ€– â‰¤ 0 := by rw [M_zero] at hM; exact hM
        have h_norm_nonneg : â€–x nâ€– â‰¥ 0 := norm_nonneg (x n)
        exact norm_le_zero_iff.mp this
      rw [Metric.tendsto_atTop]; intro Îµ Îµ_pos; use 0; intro n hn; rw [h_xn_zero n]; simpa
    Â· have h_M_pos : M > 0 := by
        specialize hM 0; push_neg at M_zero
        have h_M_nonneg : M â‰¥ 0 := by
          calc
            _ â‰¥ â€–x 0â€– := hM
            _ â‰¥ 0 := norm_nonneg (x 0)
        exact lt_of_le_of_ne h_M_nonneg (id (Ne.symm M_zero))
      have h_Îµ_pos_div : âˆ€ Îµ > 0, Îµ / M > 0 := by intros Îµ Îµ_pos; exact div_pos Îµ_pos h_M_pos
      rw [Metric.tendsto_atTop] at h_u_diff âŠ¢; intro Îµ Îµ_pos
      specialize h_u_diff (Îµ / M) (h_Îµ_pos_div Îµ Îµ_pos); obtain âŸ¨N, hNâŸ© := h_u_diff
      use N; intro n hn; specialize hN n hn; rw [Real.dist_eq]; simp only [sub_zero]
      rw [dist_eq_norm, sub_zero] at hN
      calc
        |inner â„ (x n) (u n - u_lim)|
            â‰¤ â€–x nâ€– * â€–u n - u_limâ€– := by exact abs_real_inner_le_norm (x n) (u n - u_lim)
          _ â‰¤ M * â€–u n - u_limâ€– := by
              apply mul_le_mul
              Â· exact hM n
              Â· simp
              Â· exact norm_nonneg (u n - u_lim)
              Â· linarith
          _ < M * (Îµ / M) := by apply mul_lt_mul_of_pos_left hN h_M_pos
          _ = Îµ := by field_simp [ne_of_gt h_M_pos]
  have h2: Tendsto (fun n => inner â„ (x n - x_lim) u_lim) atTop (ğ“ 0) := by
    rw [weakConverge_iff_inner_converge] at h_wkconv_x; specialize h_wkconv_x u_lim
    rw [tendsto_iff_sub_tendsto_zero_inner] at h_wkconv_x; exact h_wkconv_x
  rw [show Tendsto (fun n â†¦ inner â„ (x n) (u n)) atTop (ğ“ (inner â„ x_lim u_lim))
    â†” Tendsto (fun n â†¦ inner â„ (x n) (u n) - inner â„ x_lim u_lim) atTop (ğ“ 0) by
  constructor
  Â· intro h; convert Tendsto.sub h tendsto_const_nhds using 1; ext n; simp
  Â· intro h
    exact (tendsto_iff_sub_tendsto_zero (fun n â†¦ inner â„ (x n) (u n))
      (inner â„ x_lim u_lim)).mpr h]
  rw [eq]
  have h_add : Tendsto (fun n => inner â„ (x n) (u n - u_lim) + inner â„ (x n - x_lim) u_lim)
    atTop (ğ“ (0 + 0)) := Tendsto.add h1 h2
  convert h_add; simp
